<!DOCTYPE html>
<html>

<head>
  <title>Hướng dẫn</title>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">

  <link rel="stylesheet" href="w3.css">
  <link rel="shortcut icon" href="favicon.ico">

  <!-- Highlight code -->
  <link rel="stylesheet" href="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.15.9/styles/vs2015.min.css">
  <script src="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.15.9/highlight.min.js"></script>
  <script src="//cdnjs.cloudflare.com/ajax/libs/highlight.js/9.15.9/languages/arduino.min.js"></script>
  <script>hljs.initHighlightingOnLoad();</script>
</head>

<body>
  <div class="w3-bar w3-blue">
    <a href="/" class="w3-bar-item w3-button w3-mobile">Trang chủ</a>
    <a href="/guide" class="w3-bar-item w3-button w3-mobile">Hướng dẫn</a>
    <a href="/voice" class="w3-bar-item w3-button w3-mobile">Giọng nói</a>
  </div>
  <div class="w3-row">
    <div class="w3-container w3-col w3-hide-small m3 w3-right-align w3-indigo">
      <ul class="w3-ul">
        <li><h1>Mục lục</h1></li>
        <li><a href="#library">Cài thư viện</a></li>
        <li><a href="#example">Code mẫu</a></li>
      </ul>
    </div>
    <div class="w3-container w3-col s12 m6">
      <h1 id="library">Cài thư viện</h1>
      <p>Tải và cài 3 thư viện sau:<p>
      <ul>
        <li><a href="https://github.com/timum-viw/socket.io-client">socket.io-client</a></li>
        <li><a href="https://github.com/Links2004/arduinoWebSockets">arduinoWebSockets</a></li>
        <li><a href="https://github.com/bblanchon/ArduinoJson">ArduinoJson</a></li>
      </ul>
      <h1 id="example">Code mẫu</h1>
      <p>Các đoạn quan trọng được chú thích ở những chỗ màu xanh lá</p>
      <pre>
<code class="arduino">#include &lt;ESP8266WiFi.h&gt;
#include &lt;ArduinoJson.h&gt;
#include &lt;SocketIoClient.h&gt;

// Khai báo chân của bánh xe, A là trái, B là phải
const int AIN1 = D1;
const int AIN2 = D2;
const int BIN1 = D3;
const int BIN2 = D4;

// Khai báo tệp JSON
const int capacity = JSON_OBJECT_SIZE(2) + JSON_OBJECT_SIZE(3) + 40;
StaticJsonDocument&lt;capacity&gt; json;

// Khai báo kết nối tới socket server
SocketIoClient socket;
const char *host = "tanthongiot.herokuapp.com";
const int port = 80;

void setup()
{
  Serial.begin(115200);

  motorInit();

  // Đổi tên và mật khẩu WiFi tại đây:
  // WiFi.begin("ten WiFi", "mat khau WiFi");
  WiFi.begin("WiFi", "...");

  // Chờ kết nối
  while (WiFi.status() != WL_CONNECTED)
  {
    delay(500);
    Serial.print(".");
  }

  // In ra địa chỉ IP
  Serial.println("");
  Serial.println("WiFi connected");
  Serial.println("IP address: ");
  Serial.println(WiFi.localIP());

  // Mỗi khi nhận được sự kiện "gui-lenh" thì gọi thủ tục xử lý "event"
  socket.on("gui-lenh", event);

  // Mỗi khi nhận được sự kiện "connect" (khi kết nối tới server)
  // thì gọi lệnh xử lý này (đăng nhập tới server)
  socket.on("connect", [](const char *payload, size_t length) {
    // Đổi mật khẩu của con ESP tại đây: (càng bảo mật càng tốt)
    socket.emit("dang-nhap", "\"xewifi\"");
  });

  // Bắt đầu kết nối tới socket server
  socket.begin(host, port);
}

void loop()
{
  // Lắng nghe sự kiện từ server
  socket.loop();
}

/* Thủ tục xử lý "event"
  * Dữ liệu gửi về sẽ có dạng JSON
  * {
  *  lenh: ...
  *  giatri: ...
  * }
  */
void event(const char *payload, size_t length)
{
  // Giải mã JSON
  deserializeJson(json, payload);

  // Lấy giá trị của "lenh" trong JSON
  String lenh = json["lenh"];

  // Nếu lệnh được gửi từ các nút lệnh thì lệnh là "guilenh"
  if (lenh == "guilenh")
  {
    String giatri = json["giatri"];

    // Xử lý giá trị lệnh tại đây
    if (giatri == "toi")
    {
      out(512, 512);
    }
    else if (giatri == "lui")
    {
      out(-512, -512);
    }
    else if (giatri == "dung")
    {
      out(0, 0);
    }
    else if (giatri == "trai")
    {
      out(0, 512);
    }
    else if (giatri == "phai")
    {
      out(512, 0);
    }

    Serial.print("Nhan duoc lenh: ");
    Serial.println(giatri);
  }

  // Nếu lệnh được gửi từ thanh trượt thì lệnh là "thanhtruot"
  else if (lenh == "thanhtruot")
  {
    int giatri = json["giatri"];

    // Xử lý giá trị thanh trượt tại đây:
    Serial.print("Gia tri thanh truot: ");
    Serial.println(giatri);
  }

  // Nếu lệnh được gửi từ touchpad thì lệnh là "joystick"
  else if (lenh == "joystick")
  {
    int x = json["giatri"]["x"];
    int y = json["giatri"]["y"];
    int tocdo = json["giatri"]["tocdo"];

    // Xử lý tọa độ x, y, tốc độ tại đây:
    // Biến tốc độ từ 0 -&gt; 3 thành từ 0 -&gt; 1023
    tocdo = map(tocdo, 0, 3, 0, 1023);

    if (x &gt; 50 and x &lt;= 100)
    {
      // rẽ phải
      out(tocdo, 0);
    }
    else if (x &lt; -50 and x &gt;= -100)
    {
      // rẽ trái
      out(0, tocdo);
    }
    else
    {
      if (y &gt; 0 and y &lt;= 100)
      {
        // Chạy tới
        out(tocdo, tocdo);
      }
      else if (y == 0)
      {
        // Đứng yên
        out(0, 0);
      }
      else
      {
        // Chạy lùi
        out(-tocdo, -tocdo);
      }
    }

    Serial.print("x = ");
    Serial.print(x);
    Serial.print(" y = ");
    Serial.print(y);
    Serial.print(" tocdo = ");
    Serial.println(tocdo);
  }
}

// Hàm điều khiển bánh trái
void outLeft(int lspeed)
{
  if (lspeed &gt;= 0)
  {
    analogWrite(AIN1, lspeed);
    digitalWrite(AIN2, LOW);
  }
  else
  {
    digitalWrite(AIN1, LOW);
    analogWrite(AIN2, abs(lspeed));
  }
}

// Khai báo chân OUTPUT cho motor
void motorInit()
{
  pinMode(AIN1, OUTPUT);
  pinMode(AIN2, OUTPUT);
  pinMode(BIN1, OUTPUT);
  pinMode(BIN2, OUTPUT);
}

// Hàm điều khiển bánh phải
void outRight(int rspeed)
{
  if (rspeed &gt;= 0)
  {
    analogWrite(BIN1, rspeed);
    digitalWrite(BIN2, LOW);
  }
  else
  {
    digitalWrite(BIN1, LOW);
    analogWrite(BIN2, abs(rspeed));
  }
}

// Hàm điều khiển cả hai bánh
void out(int lspeed, int rspeed)
{
  outLeft(lspeed);
  outRight(rspeed);
}</code>
      </pre>
    </div>
  </div>
</body>

</html>
